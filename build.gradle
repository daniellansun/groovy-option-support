/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

ext {
    groovyOptionSupportVersion = '1.0.1'
}

group 'com.groovyhelp'
version "${groovyOptionSupportVersion}"

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource

}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

assemble.dependsOn sourcesJar, groovydocJar

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.groovyhelp'
            artifactId 'groovy-option-support'
            version "${groovyOptionSupportVersion}"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    configurations = ['archives']
    publish = true
    override = false
    pkg {
        repo = 'generic'
        name = 'groovy-option-support'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/danielsun1106/groovy-option-support.git'
        publicDownloadNumbers = true

        githubRepo = 'danielsun1106/groovy-option-support'
        githubReleaseNotesFile = 'README.md'

        version {
            name = "${groovyOptionSupportVersion}"
            vcsTag = "${groovyOptionSupportVersion}"
            released  = new Date()
        }
    }
}
